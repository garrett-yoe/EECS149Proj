target C {
  platform: {
    name: "rp2040",
    board: "pololu_3pi_2040_robot"
  },
  single_threaded: true
}

import Display from "lib/Display.lf"
import Motors from "lib/Motors.lf"

preamble {=
  #include <stdio.h>
  #include <pico/stdlib.h>
  #include <hardware/gpio.h>
  #include <hardware/uart.h>
  #include <string.h>

  #define UART_ID uart0
  #define BAUD_RATE 115200
  #define UART_TX_PIN 28
  #define UART_RX_PIN 29
=}

reactor UARTrec {
  input rec: bool
  output mess: string

  reaction(startup) -> mess{=
    lf_set(mess, "WAITING");
  =}

  initial mode RECEIVING  {
    reaction(rec) -> mess {=
      static char uart_buf[17];
      static int index = 0;
      if(rec->value) {
        while(uart_is_readable(UART_ID)) {
          char let = uart_getc(UART_ID);
          if (let == '\r' || let == '\n') {
            break;
          }
          uart_buf[index] = let;
          index++;
        }
        uart_buf[index] = '\0';
        index = 0;
      }
      lf_set(mess, uart_buf);
    =}
  }
}

reactor Robot {
  input com: string
  output notify: string

  motors = new Motors()

  reaction(startup) -> notify {=
    lf_set(notify, "START");
  =}

  initial mode NEED_COMMAND {
    reaction(com) -> notify, motors.left_power, motors.right_power {=
      if (strcmp(com->value, "for") == 0) {
        lf_set(notify, "DRIVE FOR");
        lf_set(motors.left_power, 0.2f);
        lf_set(motors.right_power, 0.2f);
      } else if (strcmp(com->value, "rig") == 0) {
        lf_set(notify, "DRIVE RIG");
        lf_set(motors.left_power, 0.0f);
        lf_set(motors.right_power, 0.2f);
      } else if (strcmp(com->value, "lef") == 0) {
        lf_set(notify, "DRIVE LEF");
        lf_set(motors.left_power, 0.2f);
        lf_set(motors.right_power, 0.0f);
      } else {
        lf_set(notify, com->value);
        lf_set(motors.left_power, 0.0f);
        lf_set(motors.right_power, 0.0f);
      } 
    =}
  }
}

main reactor {
  timer t(0, 1000 ms)
  state received: bool = false
  
  uart = new UARTrec()
  d = new Display()
  rob = new Robot()

  rob.notify->d.line1
  uart.mess->rob.com

  reaction(startup) {=
      stdio_init_all();
      gpio_init(PICO_DEFAULT_LED_PIN);
      gpio_set_dir(PICO_DEFAULT_LED_PIN, GPIO_OUT);
      //UART setup
      uart_init(UART_ID, BAUD_RATE);
      gpio_set_function(UART_TX_PIN, GPIO_FUNC_UART);
      gpio_set_function(UART_RX_PIN, GPIO_FUNC_UART);
  =}

  reaction(t) -> uart.rec, d.line0 {=
    static char buf0[17];
    if (uart_is_readable(UART_ID)) {
      snprintf(buf0, 17, "GOT MESS");
      lf_set(uart.rec, true);
    } else {
      snprintf(buf0, 17, "NO MESS");
    }
    lf_set(d.line0, buf0);
  =}   

}