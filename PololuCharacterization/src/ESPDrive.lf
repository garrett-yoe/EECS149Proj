target C {
  platform: {
    name: "rp2040",
    board: "pololu_3pi_2040_robot"
  },
  single_threaded: true
}

import Display from "lib/Display.lf"
import Motors from "lib/Motors.lf"

preamble {=
  #include <stdio.h>
  #include <pico/stdlib.h>
  #include <hardware/gpio.h>
  #include <hardware/uart.h>
  #include <string.h>

  #define UART_ID uart0
  #define BAUD_RATE 115200
  #define UART_TX_PIN 28
  #define UART_RX_PIN 29
=}

reactor UARTrec {
  input rec: bool
  output mess: string

  reaction(startup) -> mess{=
    lf_set(mess, "WAITING");
  =}

  initial mode RECEIVING  {
    reaction(rec) -> mess {=
      static char uart_buf[17];
      static int index = 0;
      if(rec->value) {
        while(uart_is_readable(UART_ID)) {
          char let = uart_getc(UART_ID);
          if (let == '\r' || let == '\n') {
            break;
          }
          uart_buf[index] = let;
          index++;
        }
        uart_buf[index] = '\0';
        index = 0;
        lf_set(mess, uart_buf);
      } else {
        lf_set(mess, "sty");
      }
    =}
  }
}

reactor Robot {
  input cmd: string
  output notify: string
  output debug: string

  preamble {=
    float lpower = 0.0f;
    float rpower = 0.0f;
  =}

  motors = new Motors()
  

  reaction(startup) -> notify, debug {=
    lf_set(notify, "NOTIF INIT");
    lf_set(debug, "DEBUG INIT");
  =}

  initial mode NEED_COMMAND {
    reaction(cmd) -> notify, debug, motors.left_power, 
              motors.right_power {=
      static char buf[17];
      static char deBuf[17];
      if (strcmp(cmd->value, "sty") == 0) {
        snprintf(buf, 17, "Staying");
        snprintf(deBuf, 17, "cur l:%0.1f r:%0.1f", lpower, rpower);
      } else {
        if (strcmp(cmd->value, "ope") == 0 || strcmp(cmd->value, "clo") == 0) {
          lpower = 0.2f; rpower = -0.2f;
          snprintf(deBuf, 17, "CLW -> 180");
        } else if (strcmp(cmd->value, "for") == 0) {
          lpower = 0.2f; rpower = 0.2f;
          snprintf(deBuf, 17, "new l:%0.1f r:%0.1f", lpower, rpower);
        } else if (strcmp(cmd->value, "avo") == 0) {
          lpower = 0.0f; rpower = 0.0f;
          snprintf(deBuf, 17, "AVOIDANCE");
        } else if (strcmp(cmd->value, "rig") == 0) {
          lpower = 0.15f; rpower = -0.15f;
          snprintf(deBuf, 17, "new l:%0.1f r:%0.1f", lpower, rpower);
        } else if (strcmp(cmd->value, "lef") == 0) {
          lpower = -0.15f; rpower = 0.15f;
          snprintf(deBuf, 17, "new l:%0.1f r:%0.1f", lpower, rpower);
        } else if (strcmp(cmd->value, "sto") == 0) {
          lpower = 0.0f; rpower = 0.0f;
          snprintf(deBuf, 17, "new l:%0.1f r:%0.1f", lpower, rpower);
        }
        snprintf(buf, 17, cmd->value);
      } 
      lf_set(notify, buf);
      lf_set(debug, deBuf);
      lf_set(motors.left_power, lpower);
      lf_set(motors.right_power, rpower);
    =}
  }
}

main reactor {
  timer t(0, 50 ms)
  state received: bool = false
  
  uart = new UARTrec()
  d = new Display()
  rob = new Robot()

  rob.notify->d.line1
  rob.debug->d.line2
  uart.mess->rob.cmd

  reaction(startup) {=
      stdio_init_all();
      gpio_init(PICO_DEFAULT_LED_PIN);
      gpio_set_dir(PICO_DEFAULT_LED_PIN, GPIO_OUT);
      //UART setup
      uart_init(UART_ID, BAUD_RATE);
      gpio_set_function(UART_TX_PIN, GPIO_FUNC_UART);
      gpio_set_function(UART_RX_PIN, GPIO_FUNC_UART);
  =}

  reaction(t) -> uart.rec, d.line0 {=
    static char buf0[17];
    if (uart_is_readable(UART_ID)) {
      snprintf(buf0, 17, "Connected");
      lf_set(uart.rec, true);
    } else {
      lf_set(uart.rec, false);
      snprintf(buf0, 17, "No Connection");
    }
    lf_set(d.line0, buf0);
  =}   

}
